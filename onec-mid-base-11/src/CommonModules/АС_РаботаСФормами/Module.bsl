// <<< Начало: А.С. Голубев -> 15.09.2024г.
// Задача: Программно выводим на форму доп реквизиты документов

#Область ПрограммныйИнтерфейс

// Дополнить форму полями.
// Программно выводит на форму доп реквизиты из документа
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо разместить реквизиты.
Процедура ДополнитьФормуПолями(Форма) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма);
		ДобавитьПолеИКомандуРасчетаСкидки(Форма);
	ИначеЕсли 
		Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" ИЛИ
		Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма);
	ИначеЕсли 
		Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" ИЛИ
		Форма.ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицо(Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ДобавитьПолеКонтактноеЛицо(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.АС_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицо(Форма)
	
	Если Форма.ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.АС_КонтактноеЛицо";
	ИначеЕсли Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда	
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.АС_КонтактноеЛицо"; 
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПолеИКомандуРасчетаСкидки(Форма)
	
	// добавляем группу Скидка
	Группа = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	// добавляем поле ввода
	ПолеВвода = Форма.Элементы.Добавить("Скидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.АС_Скидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "АС_СкидкаПриИзменении");
	
	// добавляем команду
	Команда = Форма.Команды.Добавить("Пересчитать");
	Команда.Заголовок = "Пересчитать со скидкой";
	Команда.Действие = "ПересчитатьСоСкидкой"; 
	
	// добавляем кнопку для команды
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитать", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "Пересчитать";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		
КонецПроцедуры

#КонецОбласти
// Конец: А.С. Голубев >>>

